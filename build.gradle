import org.apache.tools.ant.filters.ReplaceTokens

def TOMCAT_HOME = System.getenv("TC_HOME")
def TOMCAT_BIN  = System.getenv("TC_BIN")

apply plugin: 'waroverlay'
apply plugin: "name.mazgalov.vaadin.sass.compiler"

repositories {
  mavenCentral()
  maven {
      //url "https://oss.sonatype.org/content/repositories/snapshots"
      url "https://oss.sonatype.org/content/repositories/releases"
  }
}

// changes the context/environment of this build script
buildscript {
    repositories {
        jcenter()
        maven {
          url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.scaldingspoon.gradle:gradle-waroverlay-plugin:0.9.3"
        classpath "gradle.plugin.name.mazgalov:gradle-sass-vaadin-compiler:0.1.1"
    }
}

dependencies {
  runtime ('com.novartis.opensource:YADA-Quickstart:8.4.0@war') {
    transitive = true
  }
}

compileSass {
  scssFile = file("$projectDir/src/main/webapp/lib/gridvar/sass/gridvar.scss")
  cssFile = file("$projectDir/src/main/webapp/lib/gridvar/sass/gridvar.css")
}

task shutdownTomcat(type:Exec) {
  workingDir TOMCAT_BIN
  commandLine './catalina.sh', 'stop'
}

task startupTomcat(dependsOn: 'copyToServer', type:Exec) {
  workingDir TOMCAT_BIN
  commandLine './catalina.sh', 'jpda', 'start'
}

task deployLocal(dependsOn: 'startupTomcat') << {
  println "Deployment successful"
}

task cleanServer(dependsOn: 'shutdownTomcat', type:Delete) {
  delete TOMCAT_HOME+'/webapps/'+project.name
}

task copyToServer(dependsOn: ['war','cleanServer'], type:Copy) {
  from 'build/libs'
  into TOMCAT_HOME +'/webapps'
}

task buildHtmlTemplates(dependsOn: 'shutdownTomcat',type:Exec) {
  commandLine 'hulk',"$projectDir/src/main/resources/template/*.hogan"
  doFirst {
    standardOutput = new FileOutputStream(new File("$projectDir/src/main/webapp/templates.js"))
  }
}

warOverlay {
  includeWarJars true
}

war {
  from('resources')  {
        include '*.properties'
        into 'WEB-INF/classes/'
  }
  exclude '**/yada-api-7.1.2.jar'
}
war.dependsOn shutdownTomcat,compileSass,buildHtmlTemplates

/*
shutdownTomcat,
compileSass,
buildHtmlTemplates,
war,
warOverlay
*/
